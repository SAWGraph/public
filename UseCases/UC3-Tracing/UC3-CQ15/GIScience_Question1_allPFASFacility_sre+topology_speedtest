PREFIX onto: <http://www.ontotext.com/>
PREFIX us_frs: <http://sawgraph.spatialai.org/v1/us-frs#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX me_egad: <http://sawgraph.spatialai.org/v1/me-egad#>
PREFIX hyf: <https://www.opengis.net/def/schema/hy_features/hyf/>
PREFIX spatial: <http://purl.org/spatialai/spatial/spatial-full#>
PREFIX naics: <http://sawgraph.spatialai.org/v1/fio/naics#>
PREFIX fio: <http://sawgraph.spatialai.org/v1/fio#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX kwg-ont: <http://stko-kwg.geog.ucsb.edu/lod/ontology/>
PREFIX kwgr: <http://stko-kwg.geog.ucsb.edu/lod/resource/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX coso: <http://sawgraph.spatialai.org/v1/contaminoso#>
PREFIX qudt: <http://qudt.org/schema/qudt/>

SELECT * WHERE {
    SERVICE <repository:FIO> { # B1a: Retrieve  facilities and their locations
        ?facility_iri a fio:Facility ; # IRI (unique identifier) of each facility
            rdfs:label ?facility_label ; # Human-readable label (name) of each facility
        	  a us_frs:EPA-PFAS-Facility ;
            #geo:hasGeometry/geo:asWKT ?facility_wkt ; # Facility geometry as WKT string
            kwg-ont:sfWithin ?s2_cell ; # S2 cell that the facility is located in
            kwg-ont:sfWithin ?countysub .  # County subdivision the facility is in
    }
    SERVICE <repository:Spatial> { # B1b: spatially filter to State of Maine (USA.23)
        ?countysub a kwg-ont:AdministrativeRegion_3 ;
            kwg-ont:administrativePartOf+ kwgr:administrativeRegion.USA.23 .
        ?s2_cell a kwg-ont:S2Cell_Level13 .
        ?s2_neighborhood kwg-ont:sfTouches | owl:sameAs ?s2_cell . # Facility S2 cell and its 8 neighbors
            #geo:hasGeometry/geo:asWKT ?s2_wkt .  # S2 cell geometries for visualization
            #geo:hasGeometry/geo:asWKT ?s2_ds_reach_wkt . # Downstream S2 geometries
    }
    SERVICE <repository:Hydrology> { # B1c: tracing hydrological network downstream
        ?reach spatial:connectedTo ?s2_neighborhood . # Stream reaches crossing the facility S2 neighborhoods
        ?reach a hyf:HY_FlowPath ;
            hyf:downstreamWaterBody+ ?ds_reach ; # Downstream stream reaches
        #?ds_reach geo:hasGeometry/geo:asWKT ?ds_reach_wkt ; # Stream reach geometries
                spatial:connectedTo ?s2_ds_reach . # S2 cells for downstream stream reaches
        ?s2_ds_reach a kwg-ont:S2Cell_Level13 . # Downstream S2 cells								#this statement needed to be moved down from spatial repo in order for the query to run efficiently
    }
	SERVICE <repository:SAWGraph> {  # B3a
        ?sample_point a coso:SamplePoint ;
            	spatial:connectedTo ?s2_ds_reach ; # Find samplepoints within the downstream S2 cells
            	geo:hasGeometry/geo:asWKT ?sample_point_wkt ; # Get samplepoint geometries
            	me_egad:samplePointType ?sp_type .  # Get samplepoint type
        VALUES ?sp_type {me_egad:featureType.PD me_egad:featureType.RI me_egad:featureType.LK} . # Filter to relevant samplepoint types
        ?material_sample coso:fromSamplePoint ?sample_point ;  # Get the associates ample
            	coso:ofSampleMaterialType ?sample_type . # Identify type of sample
        # B3b: Identify analyzed PFAS substance and measurement value
        ?observation coso:analyzedSample ?material_sample ; # Get associated observation 
            	coso:ofSubstance ?substance ; # Get analyzed kind of PFAS
            	sosa:hasResult ?measure . # Get result of the observation
        ?measure qudt:quantityValue ?quantity_v .
        ?quantity_v qudt:numericValue ?value ; # Numeric value of the quantity
           		qudt:unit ?unit. # Unit of the quantity
       
    }
}
