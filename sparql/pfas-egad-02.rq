# Example of retrieving all surface water and fish tissue sample results (samplePointTypes me_egad:featureType.PD, me_egad:featureType.RI, me_egad:featureType.LK) from a specific sample point (me_egad_data:samplePoint.105221)
# This example is of a sample point that is downstream from a Paper Manufacturing Company in Maine.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX stad: <http://purl.org/spatialai/stad/v2/core/>

PREFIX coso: <http://w3id.org/coso/v1/contaminoso#>
PREFIX me_egad: <http://w3id.org/sawgraph/v1/me-egad#>
PREFIX me_egad_data: <http://w3id.org/sawgraph/v1/me-egad-data#>

select ?materialSample ?observation  (GROUP_CONCAT(DISTINCT(?sampleType); separator=", ") as ?sampleTypes) ?substance ?substanceL ?value ?unit
where  {
    SERVICE <repository:PFAS>
    {
        me_egad_data:samplePoint.105221 me_egad:samplePointType ?pointType.
        #VALUES ?pointType {me_egad:featureType.PD me_egad:featureType.RI me_egad:featureType.LK}
		    #?pointType rdfs:label ?ptl.
        me_egad_data:samplePoint.105221 ^coso:fromSamplePoint ?materialSample.
        ?observation coso:analyzedSample ?materialSample.
        ?materialSample coso:sampleOfMaterialType ?st. # a sample can have multiple types
        ?st rdfs:label ?sampleType.
        ?observation coso:ofSubstance ?substance.
        ?substance rdfs:label ?substanceL.
		#Filter to a specific substance: 
		# ?substance rdfs:label "PERFLUOROOCTANE SULFONIC ACID".
        ?observation coso:hasResult ?measure.
        ?measure qudt:quantityValue ?result.
        ?result qudt:numericValue ?value.
        ?result qudt:hasUnit ?unit.
        #FILTER NOT EXISTS{ ?measure a stad:StatisticalAggregateData} #remove results that are aggregates
    }
} GROUP BY ?materialSample ?observation ?substance ?substanceL ?value ?unit 
LIMIT 100

