
# Federated queries: FIO with SpatialGraph (S2L13 and AdminRegions)
# CQ 2
# Retrieve all landfills (indicated by the six-digit NAICS code 562212) in Penbobscot County. Retrieve their S2 cell location and town name.)

### GraphDB version
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX fio: <http://w3id.org/fio/v1/fio#>
PREFIX naics: <http://w3id.org/fio/v1/naics#>
PREFIX epa-frs: <http://w3id.org/fio/v1/epa-frs#>
PREFIX kwg-ont: <http://stko-kwg.geog.ucsb.edu/lod/ontology/>
PREFIX kwgr: <http://stko-kwg.geog.ucsb.edu/lod/resource/>
PREFIX spatial: <http://purl.org/spatialai/spatial/spatial-full#> 

select ?facility ?facilitylabel ?s2 ?townlabel where {
	SERVICE <repository:FIO> 
    {
     ?facility rdf:type epa-frs:EPA-PFAS-Facility ;
    		fio:ofIndustry naics:NAICS-562212 ;
        	kwg-ont:sfWithin ?town , ?s2.
       ?s2 rdf:type kwg-ont:S2Cell_Level13 .
	   ?town rdf:type kwg-ont:AdministrativeRegion_3 .
        OPTIONAL{?facility rdfs:label ?facilitylabel}
	}
    SERVICE <repository:Spatial> 
    {
        # Select towns and find the S2 cells they have in common with the above S2 cells
        ?s2 spatial:connectedTo ?town .
        ?town rdf:type kwg-ont:AdministrativeRegion_3 ;
        spatial:connectedTo* kwgr:administrativeRegion.USA.23 ;
        rdfs:label ?townlabel .
    }

} 


### FRINK version (does not return results as of 2025/09/18)
# Possibly comment out the OPTIONAL line
# Changing spatial:connectedTo* to kwg-ont:administrativePartOf/kwg-ont:administrativePartOf seems to help
# Add DISTINCT to SELECT statement
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX fio: <http://w3id.org/fio/v1/fio#>
PREFIX naics: <http://w3id.org/fio/v1/naics#>
PREFIX epa-frs: <http://w3id.org/fio/v1/epa-frs#>
PREFIX kwg-ont: <http://stko-kwg.geog.ucsb.edu/lod/ontology/>
PREFIX kwgr: <http://stko-kwg.geog.ucsb.edu/lod/resource/>
PREFIX spatial: <http://purl.org/spatialai/spatial/spatial-full#> 

SELECT ?facility ?facilitylabel ?s2 ?townlabel WHERE {
  ?facility rdf:type epa-frs:EPA-PFAS-Facility ;
            fio:ofIndustry naics:NAICS-562212 ;
            kwg-ont:sfWithin ?town , ?s2.
  ?s2 rdf:type kwg-ont:S2Cell_Level13 .
  ?town rdf:type kwg-ont:AdministrativeRegion_3 .
  OPTIONAL{?facility rdfs:label ?facilitylabel}
  # Select towns and find the S2 cells they have in common with the above S2 cells
  ?s2 spatial:connectedTo ?town .
  ?town rdf:type kwg-ont:AdministrativeRegion_3 ;
        spatial:connectedTo* kwgr:administrativeRegion.USA.23 ;
        rdfs:label ?townlabel .
} 
