# UC1-2: What surface water bodies are near (same or neighboring S2 cell) landfills or DoD sites?
# This should be run from the S2L13_Admin repository
# This query does not currently access DoD sites, only landfills

PREFIX fio: <http://sawgraph.spatialai.org/v1/fio#>
PREFIX hyf: <https://www.opengis.net/def/schema/hy_features/hyf/>
PREFIX kwg-ont: <http://stko-kwg.geog.ucsb.edu/lod/ontology/>
PREFIX naics: <http://sawgraph.spatialai.org/v1/fio/naics#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?wblabel ?lflabel WHERE {
    #SERVICE <repository:Hydrology> 
    {
        SELECT * WHERE {
            # Select waterbodies and their S2 cell overlap relations
            # Assumes landfills are not within a waterbody
            ?wb rdf:type hyf:HY_WaterBody .
            ?wb rdfs:label ?wblabel .
            ?wb_s2 kwg-ont:sfOverlaps ?wb . 
        }
    }
    
    #SERVICE <repository:FIO> 
    {
        SELECT * WHERE {
            # Find landfills and their S2 cell relations
 			?landfill a fio:Facility .
    		?landfill fio:ofIndustry naics:NAICS-IndustryCode-562212 .
            ?landfill rdfs:label ?lflabel .
            ?landfill kwg-ont:sfWithin ?lf_s2 .
        }
    }
    
    #SERVICE <repository:S2L13_AdminRegions> #Spatial
    {
    # Restrict water bodies to those with an S2 cell adjacent to a landfill cell or within a landfill cell
    ?lf_s2 kwg-ont:sfTouches ?wb_s2 .
    OPTIONAL { ?landfill kwg-ont:sfWithin ?wb_s2 . }
    }
    
} GROUP BY ?wblabel ?lflabel ORDER BY ?wblabel ?lflabel