# Federated queries: FIO with Hydrology and SpatialGraph (S2L13 and AdminRegions)
# CQ 2
# Retrieve all landfills in Penobscot county that are near (have touching S2 cell) any waterbody. 

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

PREFIX kwg-ont: <http://stko-kwg.geog.ucsb.edu/lod/ontology/>
PREFIX kwgr: <http://stko-kwg.geog.ucsb.edu/lod/resource/>

PREFIX hyf: <https://www.opengis.net/def/schema/hy_features/hyf/>

PREFIX fio: <http://sawgraph.spatialai.org/v1/fio#>
PREFIX naics: <http://sawgraph.spatialai.org/v1/fio/naics#>
PREFIX us_frs: <http://sawgraph.spatialai.org/v1/us-frs#>

SELECT DISTINCT ?facility ?wb ?wblabel WHERE {
	#SERVICE <repository:FIO> 
    {
    	?facility rdf:type us_frs:EPA-PFAS-Facility ;
    		fio:ofIndustry naics:NAICS-IndustryCode-562212.
        ?facility kwg-ont:sfWithin | kwg-ont:sfOverlaps ?s2cell .
	}
    
    #SERVICE <repository:Hydrology> 
    {
        # Select waterbodies and their S2 cell relations
    	?wb rdf:type hyf:HY_WaterBody .
    	?wb rdfs:label ?wblabel .
        # the S2cell must be the subject and the WB the object.
		?s2neighbor kwg-ont:sfWithin | kwg-ont:sfOverlaps ?wb .
    }
        
    #SERVICE <repository:S2L13_AdminRegions> 
    {
			?s2cell rdf:type kwg-ont:S2Cell_Level13 .
        	?s2cell kwg-ont:sfWithin | kwg-ont:sfOverlaps kwgr:administrativeRegion.USA.23019 .
            ?s2cell kwg-ont:sfTouches ?s2neighbor.
    }
} 